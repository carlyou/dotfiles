# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

export ZSH=$HOME/.oh-my-zsh
ZSH_THEME="robbyrussell"
plugins=(
  git
  zsh-autosuggestions
)

source $ZSH/oh-my-zsh.sh

# ===========================
# User configuration
# ===========================
set -o vi
alias lt='ls -alrt'

export PROJECT_HOME=$HOME/projects
alias projects='cd $PROJECT_HOME'
alias cdp="cd ${PROJECT_HOME}"

if [ -d "$PROJECT_HOME/models" ]; then
  export MODEL_HOME=$PROJECT_HOME/models
  alias models='cd $MODEL_HOME'
fi

alias rc='source $HOME/.zshrc'
alias cdm='cd "${$(pwd)/test/main}"'
alias cdt='cd "${$(pwd)/main/test}"'
alias cdg='cd `git rev-parse --show-toplevel`'

alias gc='git checkout'
alias gd='git diff'
alias gdc='git diff --cached'
alias gp='git pull'
alias gs='git status'

unalias gm
gm() {
# Checkout main branch (tries main, falls back to master)
  git checkout main 2>/dev/null || \
    git checkout master 2>/dev/null || \
    echo "Neither 'main' nor 'master' branch found"
}

if command -v thefuck >/dev/null 2>&1; then
  eval "$(thefuck --alias kk)"
fi

# ===========================
# Tmux
# ===========================
ta() {
  local servername="${1:-}"

  # If no servername provided, try to find one
  if [ -z "$servername" ]; then
    # First try default
    if tmux -L "default" has-session 2>/dev/null; then
      servername="default"
    else
      # Try to find any existing server with active sessions
      local socket_dir="/tmp/tmux-$(id -u)"
      if [ -d "$socket_dir" ] && [ -n "$(ls -A "$socket_dir" 2>/dev/null)" ]; then
        for socket in "$socket_dir"/*; do
          if [ -S "$socket" ]; then
            local server=$(basename "$socket")
            if tmux -L "$server" has-session 2>/dev/null; then
              servername="$server"
              break
            fi
          fi
        done
      fi

      # If still no server found, use default
      if [ -z "$servername" ]; then
        servername="default"
      fi
    fi
  fi

  if tmux -L "$servername" has-session 2>/dev/null; then
    tmux -L "$servername" attach-session
  else
    tmux -L "$servername" new-session
  fi
}

alias ta!='ta default'

tl() {
  local socket_dir="/tmp/tmux-$(id -u)"
  if [ -d "$socket_dir" ] && [ -n "$(ls -A "$socket_dir" 2>/dev/null)" ]; then
    echo "Active tmux servers:"
    for socket in "$socket_dir"/*; do
      if [ -S "$socket" ]; then
        local servername=$(basename "$socket")
        local session_count=$(tmux -L "$servername" list-sessions 2>/dev/null | wc -l)
        echo "  $servername ($session_count sessions)"
      fi
    done
  else
    echo "No tmux servers found"
  fi
}

# ===========================
# NVM
# ===========================
export NVM_DIR="$([ -z "${XDG_CONFIG_HOME-}" ] && printf %s "${HOME}/.nvm" || printf %s "${XDG_CONFIG_HOME}/nvm")"
[ -s "$NVM_DIR/nvm.sh" ] && \. "$NVM_DIR/nvm.sh" # This loads nvm

# ===========================
# PROMPT
# ===========================
# Platform-specific Powerlevel10k paths
if [[ "$OSTYPE" == "darwin"* ]]; then
  # macOS (Homebrew)
  if brew list powerlevel10k &> /dev/null; then
    source /opt/homebrew/share/powerlevel10k/powerlevel10k.zsh-theme
    # To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
    [[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh
    [[ ! -f ~/.config/p10k/p10k.zsh ]] || source ~/.config/p10k/p10k.zsh
  else
    echo -e "\033[34m powerlevel10k not installed, using default prompt.\033[0m"
  fi
else
  # Linux (Oh My Zsh custom theme)
  if [ -f ~/.oh-my-zsh/custom/themes/powerlevel10k/powerlevel10k.zsh-theme ]; then
    source ~/.oh-my-zsh/custom/themes/powerlevel10k/powerlevel10k.zsh-theme
    # To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
    [[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh
    [[ ! -f ~/.config/p10k/p10k.zsh ]] || source ~/.config/p10k/p10k.zsh
  else
    echo -e "\033[34m powerlevel10k not installed, using default prompt.\033[0m"
  fi
fi


# ===========================
# jupyter
# ===========================
jupyter_server() {
  JUPYTER_PORT="${1:-8888}"
  SESSION_NAME="[jupyter]"
  if TMUX= tmux has-session -t "$SESSION_NAME" 2>/dev/null; then
    echo "Jupyter server already running in tmux session '$SESSION_NAME'."
  else
    TMUX= tmux new-session -s "$SESSION_NAME" -n "$JUPYTER_PORT" -d  jupyter lab --no-browser --ip=127.0.0.1 --port="$JUPYTER_PORT"
    echo "Jupyter server started in tmux session $SESSION_NAME on port $JUPYTER_PORT"
  fi
}

# ===========================
# neovim
# ===========================
vim_listen() {
  NVIM_PORT="${1:-}"
  SESSION_NAME="[nvim]"

  # Auto-select unused port if not provided
  if [ -z "$NVIM_PORT" ]; then
    # Let OS assign an unused port
    NVIM_PORT=$(python3 -c 'import socket; s=socket.socket(); s.bind(("", 0)); print(s.getsockname()[1]); s.close()')
  fi

  echo "Neovim listening on port $NVIM_PORT"

  # Create session if it doesn't exist, otherwise create new window
  if TMUX= tmux has-session -t "$SESSION_NAME" 2>/dev/null; then
    TMUX= tmux new-window -t "$SESSION_NAME" -n "$NVIM_PORT" nvim --headless --listen "0.0.0.0:$NVIM_PORT"
  else
    TMUX= tmux new-session -s "$SESSION_NAME" -n "$NVIM_PORT" -d nvim --headless --listen "0.0.0.0:$NVIM_PORT"
  fi
}


# Connect to neovim server with neovide
vim_connect() {
  NVIM_HOST=$1
  echo "Neovim connecting to host $NVIM_HOST"
  if [ -z "$NVIM_HOST" ]; then
    echo "Usage: nvim_connect <host>"
    return 1
  fi

  neovide --fork --server "$NVIM_HOST"
}

alias vim='nvim'
alias vl='vim_listen'
alias vc='vim_connect'
alias nv='neovide --fork'

# ===========================
# fzf
# ===========================
cdf() {
  _file=$(fzf)
  [ -n "$_file" ] && cd "$(dirname "$_file")"
}

cpf() {
  _file=$(fzf)
  if [ -n "$_file" ]; then
    # Strip whitespace and newlines, print to stdout
    echo "$_file" | tr -d '[:space:]'
    # Copy to clipboard if available
    if command -v pbcopy &> /dev/null; then
      echo "$_file" | tr -d '[:space:]' | pbcopy
    elif command -v xclip &> /dev/null; then
      echo "$_file" | tr -d '[:space:]' | xclip -selection clipboard
    fi
  fi
}

pdf() {
  _file=$(fzf)
  [ -n "$_file" ] && nohup zathura "$_file" &>/dev/null &
}

nf() {
  _file=$(fzf)
  # strip whitespace and newlines
  [ -n "$_file" ] && eval "vim $_file"
}

# Set up fzf key bindings and fuzzy completion
[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh
source <(fzf --zsh)

# ===========================
# SSH
# ===========================
dgx() {
  # Check if dgx-spark.local is reachable
  if ping -c 1 -W 1 dgx-spark.local &>/dev/null; then
    echo "Connecting to dgx-spark.local..."
    ssh dgx-spark-local
  # Otherwise check if dgx-spark (tailscale) is reachable
  elif ping -c 1 -W 1 dgx-spark &>/dev/null; then
    echo "Connecting to dgx-spark (tailscale)..."
    ssh dgx-spark
  else
    echo "dgx-spark is not reachable, checking Tailscale status..."
    echo "$(tailscale status)"
    return 1
  fi
}

# Nvidia / CUDA
# ===========================
# if /usr/local/cuda folder exists, add it to path
if [ -d /usr/local/cuda ]; then
  export CUDA_HOME="/usr/local/cuda"
  export TRITON_PTXAS_PATH="${CUDA_HOME}/bin/ptxas"
  export LD_LIBRARY_PATH="/usr/local/cuda/lib64:${LD_LIBRARY_PATH:+:$LD_LIBRARY_PATH}"
  export PATH="$CUDA_HOME/bin:$PATH"
fi

# ===========================
# Extra local configurations
# ===========================
export PATH="$HOME/.local/bin:$PATH"
[ -f ~/.zshrc_local ] && source ~/.zshrc_local

# ===========================
# Python
# Make sure this is executed at the bottom, so that PATH is preserved properly across venvs
# ===========================
alias p3='python3'
alias jl='jupyter lab'
alias act='source .venv/bin/activate'
alias deact='deactivate'

# Change to the parent directory of the currently active virtual environment
cduv() {
  if [ -n "$VIRTUAL_ENV" ]; then
    cd "$(dirname "$VIRTUAL_ENV")"
  else
    echo "No virtual environment is currently active"
    return 1
  fi
}

# if default venv exists
if [ -f ${XDG_DATA_HOME:-$HOME/.local/share}/venvs/default/bin/activate ]; then
  alias cdpy="cd ${PROJECT_HOME}/dotfiles/_venvs/default"
  source ${XDG_DATA_HOME:-$HOME/.local/share}/venvs/default/bin/activate
fi

